title Push Space

background_color #292929
norepeat_action

========
OBJECTS
========

( Blue Side
#059CC0 wall
#025B70 player
#037590
#27A6C4
#4DB5CD floor
)

BPlayerOn
#025B70
00000
00000
00.00
00000
00000

BPlayerOff
transparent

BTarget
#025B70
.....
.....
..0..
.....
.....

BFloor
#4DB5CD

BWall
#059CC0


( Orange Side
#FF7F00 wall 
#B55A00 player
#E87300
#FF952C
#FFAC5B floor
)

OPlayerOn
#B55A00
00000
00000
00.00
00000
00000

OPlayerOff
transparent

OTarget
#B55A00
.....
.....
..0..
.....
.....

OFloor
#FFAC5B

OWall
#FF7F00

( Utility objects )

Background ( Should never be seen. )
black

BBlack
#292929

OBlack
#292929

Tutorial
transparent

=======
LEGEND
=======

BPlayer = BPlayerOn or BPlayerOff
OPlayer = OPlayerOn or OPlayerOff

Player = BPlayerOn or OPlayerOn
PlayerOff = BPlayerOff or OPlayerOff
Floor = BFloor or OFloor
Wall = BWall or OWall
Target = BTarget or OTarget
Black = BBlack or OBlack

(
It seems strange that everything is organized as APlayer then BPlayer yet
the starting player is B.

Also what's the motivation for picking some of the symbols? Like BPlayer is B but APlayer is T? Also a mix of
symbols and a letter for the targets. Would be nice if there was a clear pattern so it was easy to make and understand
maps. Something like A -> 1 and B -> 2 as targets in their own worlds. If their target is in the other's world then
A -> ! or B -> @.
)

( hold shift to switch sides )
( O and B for the active actor on each side )
( 0 and 8 for the inactive because they resemble 0 and 8 )

( ' and ; for the active player's target ) ( on floor and wall respectively )
( / and . for the inactive player's target )

( orange side active )
. = OFloor
O = OFloor and OPlayerOn
' = OFloor and OTarget
/ = OFloor and BTarget

3 = OWall
8 = OWall and BPlayerOff
; = OWall and OTarget
] = OWall and BTarget

( blue side active )
> = BFloor
B = BFloor and BPlayerOn
" = BFloor and BTarget
? = BFloor and OTarget

# = BWall
0 = BWall and OPlayerOff
: = BWall and BTarget
} = BWall and OTarget

X = BBlack
z = BBlack and Tutorial

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Floor
BWall, BPlayer, BBlack
OWall, OPlayer, OBlack
Target, tutorial

======
RULES
======

( the starting player can be alone in a level )
( in this case, we want to prevent them from using the action button )
[ action OPlayerOn ] [ BPlayerOff ] -> [ OPlayerOn ] [ action BPlayerOff ]
[ action OPlayerOn ] -> [ OPlayerOn ]

( restore the action if the player is not alone )
[ action BPlayerOff ] [ OPlayerOn ] -> [ BPlayerOff ] [ action OPlayerOn ]

( when the player presses 'x', switch paradigms )

[ action BPlayerOn ] [ BBlack ] -> [ action BPlayerOn ] [ OBlack ]
[ action BPlayerOn ] [ BWall ] -> [ action BPlayerOn ] [ OFloor ]
[ action BPlayerOn ] [ BFloor ] -> [ action BPlayerOn ] [ OWall ]
[ action BPlayerOn ] [ OPlayerOff ] -> [ BPlayerOff ] [ OPlayerOn ]
[ action OPlayerOn ] [ OBlack ] -> [ action OPlayerOn ] [ BBlack ]
[ action OPlayerOn ] [ OWall ] -> [ action OPlayerOn ] [ BFloor ]
[ action OPlayerOn ] [ OFloor ] -> [ action OPlayerOn ] [ BWall ]
[ action OPlayerOn ] [ BPlayerOff ] -> [ OPlayerOff ] [ BPlayerOn ]

( players can push walls, and inactive players within )
( this group loops endlessly if your first move is to push a wall )
rigid [ > Player | Wall ] -> [ > Player | > Wall ]
+ rigid [ > Player | PlayerOff ] -> [ > Player | > PlayerOff ]

( after walls are moved, ensure either a floor or wall is in every cell )
late [ BPlayerOn ] [ Floor Wall ] -> [ BPlayerOn ] [ Wall ]
late [ BPlayerOn ] [ no Floor no Wall no Black] -> [ BPlayerOn ] [ BFloor ]
late [ OPlayerOn ] [ Floor Wall ] -> [ OPlayerOn ] [ Wall ]
late [ OPlayerOn ] [ no Floor no Wall no Black] -> [ OPlayerOn ] [ OFloor ]

( display the 'z' to undo message in tutorial level ) 
down [ tutorial ] [ OPlayerOff | black ] -> [ ] [ OPlayerOff | black ] message press 'z' to undo
left [ tutorial ] [ OPlayerOff | black ] -> [ ] [ OPlayerOff | black ] message press 'z' to undo
right [ tutorial ] [ OPlayerOff no target | black ] -> [ ] [ OPlayerOff | black ] message press 'z' to undo

==============
WINCONDITIONS
==============

all BPlayer on BTarget
all OPlayer on OTarget

