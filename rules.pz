title Push Space
homepage pushspace.io

background_color #292929
key_repeat_interval 0.2
norepeat_action

========
OBJECTS
========

( Player A/Blue
#059CC0 wall
#025B70 player
#037590
#27A6C4
#4DB5CD floor
)

APlayerOn
#025B70
00000
00000
00.00
00000
00000

APlayerOff
transparent

ATarget
#025B70
.....
.....
..0..
.....
.....

AFloor
#4DB5CD

AWall
#059CC0


( Player B/Orange
#FF7F00 wall 
#B55A00 player
#E87300
#FF952C
#FFAC5B floor
)

BPlayerOn
#B55A00
00000
00000
00.00
00000
00000

BPlayerOff
transparent

BTarget
#B55A00
.....
.....
..0..
.....
.....

BFloor
#FFAC5B

BWall
#FF7F00

( Combined )

ABTarget
#123456
.....
.....
..0..
.....
.....

( Utility objects )

Background ( Should never be seen. )
black

ABlack
#292929

BBlack
#292929

AWhite
#FFFFFF

BWhite
#FFFFFF

=======
LEGEND
=======

APlayer = APlayerOn or APlayerOff
BPlayer = BPlayerOn or BPlayerOff

Player = APlayerOn or BPlayerOn
PlayerOff = APlayerOff or BPlayerOff
Floor = AFloor or BFloor
Wall = AWall or BWall
Target = ATarget or BTarget or ABTarget
Black = ABlack or BBlack
White = AWhite or BWhite

(
It seems strange that everything is organized as APlayer then BPlayer yet
the starting player is B.

Also what's the motivation for picking some of the symbols? Like BPlayer is B but APlayer is T? Also a mix of
symbols and a letter for the targets. Would be nice if there was a clear pattern so it was easy to make and understand
maps. Something like A -> 1 and B -> 2 as targets in their own worlds. If their target is in the other's world then
A -> ! or B -> @.
)
. = BFloor
B = BFloor and BPlayerOn
+ = BFloor and BTarget
* = BFloor and ATarget

# = BWall
T = BWall and APlayerOff
P = BWall and BTarget
$ = BWall and ATarget

& = ABTarget

X = BBlack
W = BWhite

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Floor, AWhite, BWhite
AWall, APlayer, ABlack
BWall, BPlayer, BBlack
Target

======
RULES
======

( the starting player can be alone in a level )
( in this case, we want to prevent them from using the action button )
[ action BPlayerOn ] [ APlayerOff ] -> [ BPlayerOn ] [ action APlayerOff ]
[ action BPlayerOn ] -> [ BPlayerOn ]

( restore the action if the player is not alone )
[ action APlayerOff ] [ BPlayerOn ] -> [ APlayerOff ] [ action BPlayerOn ]

( when the player presses 'x', switch paradigms )

[ action APlayerOn ] [ ABlack ] -> [ action APlayerOn ] [ BBlack ]
[ action APlayerOn ] [ AWhite ] -> [ action APlayerOn ] [ BWhite ]
[ action APlayerOn ] [ AWall ] -> [ action APlayerOn ] [ BFloor ]
[ action APlayerOn ] [ AFloor ] -> [ action APlayerOn ] [ BWall ]
[ action APlayerOn ] [ BPlayerOff ] -> [ APlayerOff ] [ BPlayerOn ]
[ action BPlayerOn ] [ BBlack ] -> [ action BPlayerOn ] [ ABlack ]
[ action BPlayerOn ] [ BWhite ] -> [ action BPlayerOn ] [ AWhite ]
[ action BPlayerOn ] [ BWall ] -> [ action BPlayerOn ] [ AFloor ]
[ action BPlayerOn ] [ BFloor ] -> [ action BPlayerOn ] [ AWall ]
[ action BPlayerOn ] [ APlayerOff ] -> [ BPlayerOff ] [ APlayerOn ]

( players can push walls, and inactive players within )
( this group loops endlessly if your first move is to push a wall )
rigid [ > Player | Wall ] -> [ > Player | > Wall ]
+ rigid [ > APlayer | AWall BPlayerOff ] -> [ > APlayer | > AWall > BPlayerOff ]
+ rigid [ > BPlayer | BWall APlayerOff ] -> [ > BPlayer | > BWall > APlayerOff ]

( after walls are moved, ensure either a floor or wall is in every cell )
late [ APlayerOn ] [ Floor Wall ] -> [ APlayerOn ] [ Wall ]
late [ APlayerOn ] [ no Floor no Wall no Black no White] -> [ APlayerOn ] [ AFloor ]
late [ BPlayerOn ] [ Floor Wall ] -> [ BPlayerOn ] [ Wall ]
late [ BPlayerOn ] [ no Floor no Wall no Black no White] -> [ BPlayerOn ] [ BFloor ]

==============
WINCONDITIONS
==============

all APlayer on ATarget
(or ABTarget)
all BPlayer on BTarget
(or ABTarget)
